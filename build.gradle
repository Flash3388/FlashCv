plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

apply from: rootProject.file('extras.gradle')

group = GROUP
version = VERSION

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.flash3388.flashlib', name: 'flashlib', version: '2.0.0'

    implementation group: 'org.openpnp', name: 'opencv', version: '3.2.0-0'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.7.22'
}

jar {
    archiveBaseName.set(project.name)
}

task createSources(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource

    archiveBaseName.set(project.name)
    archiveClassifier.set('sources')
}

task createJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir

    archiveBaseName.set(project.name)
    archiveClassifier.set('javadoc')
}

task flashlibCvAll(type: Jar, dependsOn: build) {
    dependsOn createSources
    dependsOn createJavadoc

    archiveFileName.set('flashlib-cv-all.jar')
    destinationDirectory.set(rootProject.buildDir)

    from sourceSets.main.output
    from sourceSets.main.allSource
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenFlashlib(MavenPublication) {
            from components.java

            artifact createJavadoc
            artifact createSources

            pom {
                name = 'FlashCv'
                description = 'OpenCV Plugin for Flashlib'
                url = 'https://github.com/Flash3388/FlashCv'

                licenses {
                    license {
                        name = 'BSD 3-Clause License'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }

                developers {
                    developer {
                        id = 'tomtzook'
                        name = 'Tom Tzook'
                        email = 'tomtzook@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/Flash3388/FlashCv.git'
                    developerConnection = 'scm:git:ssh://github.com/Flash3388/FlashCv.git'
                    url = 'https://github.com/Flash3388/FlashCv'
                }
            }
        }
    }

    repositories {
        maven {
            if (isReleaseBuild()) {
                name = 'NexusStaging'
                url = NEXUS_RELEASE_REPOSITORY_URL
            } else {
                name = 'NexusSnapshot'
                url = NEXUS_SNAPSHOT_REPOSITORY_URL
            }

            credentials {
                username getNexusUsername()
                password getNexusPassword()
            }
        }
    }
}

if (shouldSign()) {
    signing {
        sign publishing.publications.mavenFlashlib
    }
}