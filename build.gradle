plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = GROUP
version = VERSION

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.flash3388,util', name: 'javabeans', version: '1.0'

    compile group: 'edu.flash3388.flashlib', name: 'flashlib', version: '1.3'

    compile group: 'org.openpnp', name: 'opencv', version: '3.2.0-0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.7.22'
}

jar {
    baseName 'flashlib-cv'
}

task createSources(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    baseName 'flashlib-cv'
    classifier = 'sources'
}

task createJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    baseName 'flashlib-cv'
    classifier = 'javadoc'
}

artifacts {
    archives createSources
    archives createJavadoc
}

publishing {
    publications {
        mavenFlashlib(MavenPublication) {
            from components.java

            artifact createJavadoc
            artifact createSources

            pom {
                name = 'FlashCv'
                description = 'OpenCV Plugin for Flashlib'
                url = 'https://github.com/Flash3388/FlashCv'

                licenses {
                    license {
                        name = 'BSD 3-Clause License'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }

                developers {
                    developer {
                        id = 'tomtzook'
                        name = 'Tom Tzook'
                        email = 'tomtzook@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/Flash3388/FlashCv.git'
                    developerConnection = 'scm:git:ssh://github.com/Flash3388/FlashCv.git'
                    url = 'https://github.com/Flash3388/FlashCv'
                }
            }
        }
    }

    repositories {
        maven {
            if (isReleaseBuild()) {
                name = 'NexusStaging'
                url = NEXUS_RELEASE_REPOSITORY_URL
            } else {
                name = 'NexusSnapshot'
                url = NEXUS_SNAPSHOT_REPOSITORY_URL
            }

            credentials {
                username ''
                password ''
            }
        }
    }
}

signing {
    sign publishing.publications.mavenFlashlib
}